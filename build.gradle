plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.30-M1'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.10.0'
}

group 'me.fungames'
version '3.6.5'

sourceCompatibility = 1.8

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "JFortniteParse"
            groupId = group
            version = version
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    implementation 'androidx.collection:collection-ktx:1.1.0'
    implementation 'com.github.FabianFG:JOodle:1.2'
    implementation 'com.github.FabianFG:KotlinASTC:1.3'
    implementation 'com.github.FabianFG:KotlinPointers:1.1'
    //implementation 'com.github.FabianFG:KotlinSquish:1.0' Somehow broken with alpha values
    implementation 'com.github.memo33:jsquish:2.0.1'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.tomgibara.bits:bits:2.1.0'
    implementation 'io.github.microutils:kotlin-logging:2.0.10'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.objenesis:objenesis:3.2'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                '-Xallow-result-return-type',
                '-Xno-call-assertions',
                '-Xno-param-assertions',
                '-Xno-receiver-assertions',
                '-Xopt-in=kotlin.ExperimentalUnsignedTypes'
        ]
    }
}